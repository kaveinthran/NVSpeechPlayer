import os

Import([
	'env',
])

# Build nvSpeechPlayer (combined dual-mode add-on)
# Copy all required files to build directory
for filePath in ("#data.py","#ipa.py","#speechPlayer.py","#speechPlayer.dll"):
	f=File(filePath)
	env.Command("synthDrivers/nvSpeechPlayer/%s"%os.path.basename(f.abspath),f,Copy("$TARGET","$SOURCE"))

# Copy __init__.py directly from source directory
env.Command("synthDrivers/nvSpeechPlayer/__init__.py","#nvdaAddon/synthDrivers/nvSpeechPlayer/__init__.py",Copy("$TARGET","$SOURCE"))

# Create manifest.ini at root
env.Textfile("manifest.ini",File("manifest-standard.ini.in"),SUBST_DICT={'_version_':env['version'],'_author_':env['author']})

# Create the add-on package
addonRelPath=Dir('.')
addon=env.ZipArchive(
	target="nvSpeechPlayer_%s.nvda-addon"%env['version'],
	source=[Dir('synthDrivers'),'manifest.ini'],
	relativeTo=addonRelPath
)
env.Depends(addon,'synthDrivers/nvSpeechPlayer/__init__.py')

Return(['addon'])
